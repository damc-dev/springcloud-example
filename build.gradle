buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        mavenCentral()
    }
    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.4.RELEASE'
        classpath('gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.19.2')
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.5.9.RELEASE"
    }
}

version '1.0-SNAPSHOT'

allprojects {
    apply plugin: 'io.spring.dependency-management'
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'idea'
    apply plugin: 'com.palantir.docker'
    apply plugin: 'org.springframework.boot'

    repositories {
        mavenCentral()
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencyManagement {
        dependencies {
            dependency 'org.codehaus.groovy:groovy-all:2.4.7'
            dependency 'junit:junit:4.12'
            dependency 'org.spockframework:spock-core:1.1-groovy-2.4'
            dependency 'org.spockframework:spock-spring:1.1-groovy-2.4'
            dependency 'cglib:cglib-nodep:3.2.4'

        }

        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Edgware.SR2'
        }
    }

    docker {
        name "${project.group}/${jar.baseName}"
        tags 'latest'
        dockerfile file('../Dockerfile')
        files jar.archivePath
        buildArgs(['JAR_FILE': "${jar.archiveName}"])
    }
}